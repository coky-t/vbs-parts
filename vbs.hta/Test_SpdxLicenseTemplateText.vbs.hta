<html>

<head>
<title>Test_SpdxLicenseTemplateText.vbs.hta</title>

<meta http-equiv="msthemecompatible" content="yes">

<hta:application innerborder="no" scroll="no" />

<script language="vbscript">
<!--
Option Explicit

'
' Copyright (c) 2020,2023 Koki Takeyama
'
' Permission is hereby granted, free of charge, to any person obtaining
' a copy of this software and associated documentation files (the "Software"),
' to deal in the Software without restriction, including without limitation
' the rights to use, copy, modify, merge, publish, distribute, sublicense,
' and/or sell copies of the Software, and to permit persons to whom the
' Software is furnished to do so, subject to the following conditions:
'
' The above copyright notice and this permission notice shall be included
' in all copies or substantial portions of the Software.
'
' THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
' IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
' FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
' AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
' LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
' FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
' IN THE SOFTWARE.
'

'
' SPDX License List Matching Guidelines, v2.3
' https://spdx.github.io/spdx-spec/v2.3/license-matching-guidelines-and-templates/
'

Public Function GetMatchingText(TemplateText)
    If TemplateText = "" Then Exit Function
    
    ' B.15.3 Legacy Text Template format
    'Const Pattern = "(?:<<var;name=""([^""]+)"";original=""(.+)"";match=""(.+)"">>|<<beginOptional>>(.+)<<endOptional>>)"
    Const Pattern = "(?:<<var;name=""([^""]+)"";original=""|<<beginOptional>>)"
    
    If Not RegExp_Test(TemplateText, Pattern, True, False) Then
        GetMatchingText = GetSimpleMatchingPattern(TemplateText)
        Exit Function
    End If
    
    Dim Matches
    Set Matches = RegExp_Execute(TemplateText, Pattern, True, False, False)
    
    Dim Match
    Set Match = Matches.Item(0)
    
    Dim PreviousPattern
    If Match.FirstIndex > 0 Then
        Dim PreviousText
        PreviousText = Left(TemplateText, Match.FirstIndex)
        PreviousPattern = GetSimpleMatchingPattern(PreviousText)
    End If
    
    Dim MiddlePattern
    Dim MiddleTextTemp
    Dim PostPattern
    Dim PostTextTemp
    
    If Match.SubMatches.Item(0) <> "" Then
        ' B.3.4 Guideline: replaceable text
        ' B.8 Bullets and numbering
        ' B.11 Copyright notice
        ' <<var;name="([^"]+)";original="(.+)";match="(.+)">>
        
        'Dim VarName
        'Dim VarOriginal
        'Dim VarMatch
        'VarName = Match.SubMatches.Item(0)
        'VarOriginal = Match.SubMatches.Item(1)
        'VarMatch = Match.SubMatches.Item(2)
        
        'MiddlePattern = VarMatch
        
        'If Match.FirstIndex + Match.Length < Len(TemplateText) Then
        '    PostTextTemp = _
        '        Right( _
        '            TemplateText, _
        '            Len(TemplateText) - (Match.FirstIndex + Match.Length))
        '    PostPattern = GetMatchingText(PostTextTemp)
        'End If
        
        Dim VarMatchPos
        Dim VarEndPos
        
        VarMatchPos = _
            InStr( _
                Match.FirstIndex + Match.Length + 1, _
                TemplateText, _
                """;match=""")
        
        If VarMatchPos > 0 Then
            VarEndPos = _
                InStr( _
                    VarMatchPos + Len(""";match="""), _
                    TemplateText, _
                    """>>")
        End If
        
        If VarEndPos > 0 Then
            MiddleTextTemp = _
                Mid( _
                    TemplateText, _
                    VarMatchPos + Len(""";match="""), _
                    VarEndPos - (VarMatchPos + Len(""";match=""")))
            MiddlePattern = MiddleTextTemp
            
            If VarEndPos - 1 + Len(""">>") < Len(TemplateText) Then
                PostTextTemp = _
                    Right( _
                        TemplateText, _
                        Len(TemplateText) - (VarEndPos - 1 + Len(""">>")))
                PostPattern = GetMatchingText(PostTextTemp)
            End If
            
        End If
        
    Else
        ' B.3.5 Guideline: omittable text
        ' B.12 License name or title
        ' B.13 Extraneous text at the end of a license
        ' "<<beginOptional>>(.+)<<endOptional>>"
        
        'Dim OptText
        'OptText = Match.SubMatches.Item(3)
        
        'Dim OptPattern
        'OptPattern = GetSimpleMatchingPattern(OptText)
        
        'MiddlePattern = "(?:" & OptPattern & ")?"
        
        'If Match.FirstIndex + Match.Length < Len(TemplateText) Then
        '    PostTextTemp = _
        '        Right( _
        '            TemplateText, _
        '            Len(TemplateText) - (Match.FirstIndex + Match.Length))
        '    PostPattern = GetMatchingText(PostTextTemp)
        'End If
        
        Dim EndOptionalPos
        'EndOptionalPos = _
        '    InStr( _
        '        Match.FirstIndex + Match.Length + 1, _
        '        TemplateText, _
        '        "<<endOptional>>")
        EndOptionalPos = _
            GetEndOptionalPos( _
                Match.FirstIndex + Match.Length + 1, _
                TemplateText)
        If EndOptionalPos > 0 Then
            MiddleTextTemp = _
                Mid( _
                    TemplateText, _
                    Match.FirstIndex + Match.Length + 1, _
                    EndOptionalPos - 1 - (Match.FirstIndex + Match.Length))
            MiddlePattern = "(?:" & GetMatchingText(MiddleTextTemp) & ")?"
            
            If EndOptionalPos - 1 + Len("<<endOptional>>") < _
                Len(TemplateText) Then
                PostTextTemp = _
                    Right( _
                        TemplateText, _
                        Len(TemplateText) - _
                            (EndOptionalPos - 1 + Len("<<endOptional>>")))
                PostPattern = GetMatchingText(PostTextTemp)
            End If
            
        End If
        
    End If
    
    GetMatchingText = PreviousPattern & MiddlePattern & PostPattern
End Function

Private Function GetEndOptionalPos(StartPos, TemplateText)
    Dim BeginOptionalPos
    BeginOptionalPos = InStr(StartPos, TemplateText, "<<beginOptional>>")
    
    Dim EndOptionalPos
    EndOptionalPos = InStr(StartPos, TemplateText, "<<endOptional>>")
    
    If BeginOptionalPos = 0 Then
        GetEndOptionalPos = EndOptionalPos
        Exit Function
    End If
    
    If BeginOptionalPos > EndOptionalPos Then
        GetEndOptionalPos = EndOptionalPos
        Exit Function
    End If
    
    Dim EndOptionalPosTemp
    EndOptionalPosTemp = _
        GetEndOptionalPos( _
            BeginOptionalPos + Len("<<beginOptional>>"), _
            TemplateText)
    
    EndOptionalPos = _
        GetEndOptionalPos( _
            EndOptionalPosTemp + Len("<<endOptional>>"), _
            TemplateText)
    
    GetEndOptionalPos = EndOptionalPos
End Function

'Attribute VB_Name = "SpdxLicenseText"
'Option Explicit

'
' Copyright (c) 2020,2022 Koki Takeyama
'
' Permission is hereby granted, free of charge, to any person obtaining
' a copy of this software and associated documentation files (the "Software"),
' to deal in the Software without restriction, including without limitation
' the rights to use, copy, modify, merge, publish, distribute, sublicense,
' and/or sell copies of the Software, and to permit persons to whom the
' Software is furnished to do so, subject to the following conditions:
'
' The above copyright notice and this permission notice shall be included
' in all copies or substantial portions of the Software.
'
' THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
' IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
' FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
' AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
' LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
' FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
' IN THE SOFTWARE.
'

'
' SPDX License List Matching Guidelines, v2.1
' https://spdx.org/spdx-license-list/matching-guidelines
'

Public Function GetSimpleMatchingPattern(LicenseText)
    Dim TempString
    TempString = LCase(LicenseText)
    
    ' 3. Whitespace
    ' 5. Punctuation
    ' 5.1.1 Guideline: Punctuation
    ' 5.1.2 Guideline: Hyphens, Dashes
    ' 5.1.3 Guideline: Quotes
    ' 6. Code Comment Indicators
    TempString = RegExpReplace(TempString, "\W*", "\W+")
    
    ' 8. Varietal Word Spelling
    TempString = RegExpReplaceWords(TempString)
    
    ' 13. HTTP Protocol
    TempString = RegExpReplace(TempString, "https?", "https?")
    
    GetSimpleMatchingPattern = TempString
End Function

'
' 8. Varietal Word Spelling
'
' | Word1 | Word2 | MatchingPattern |
' | --- | --- | --- |
' | acknowledgement | acknowledgment | acknowledge?ment |
' | analog | analogue | analog(?:ue)? |
' | analyze | analyse | analy[zs]e |
' | artifact | artefact | art[ie]fact |
' | authorization | authorisation | authori[zs]ation |
' | authorized | authorised | authori[zs]ed |
' | caliber | calibre | calib(?:er|re) |
' | canceled | cancelled | cancell?ed |
' | capitalizations | capitalisations | capitali[zs]ations |
' | catalog | catalogue | catalog(?:ue)? |
' | categorize | categorise | categori[zs]e |
' | center | centre | cent(?:er|re) |
' | copyright holder | copyright owner | copyright\W+(?:hold|own)er |
' | emphasized | emphasised | emphasi[zs]ed |
' | favor | favour | favou?r |
' | favorite | favourite | favou?rite |
' | fulfill | fulfil | fulfill? |
' | fulfillment | fulfilment | fulfill?ment |
' | Initialize | initialise | initiali[zs]e |
' | judgement | judgment | judge?ment |
' | labeling | labelling | labell?ing |
' | labor | labour | labou?r |
' | license | licence | licen[sc]e |
' | maximize | maximise | maximi[zs]e |
' | modeled | modelled | modell?ed |
' | modeling | modelling | modell?ing |
' | noncommercial | non-commercial | non-?commercial |
' | offense | offence | offen[sc]e |
' | optimize | optimise | optimi[zs]e |
' | organization | organisation | organi[zs]ation |
' | organize | organise | organi[zs]e |
' | percent | per cent | per\s*cent |
' | practice | practise | practi[cs]e |
' | program | programme | program(?:me)? |
' | realize | realise | reali[zs]e |
' | Recognize | recognise | recogni[zs]e |
' | signaling | signalling | signall?ing |
' | sublicense | sub-license | sub(?: |-)?licen[sc]e |
' | sub-license | sub license | sub(?: |-)?licen[sc]e |
' | sublicense | sub license | sub(?: |-)?licen[sc]e |
' | utilization | utilisation | utili[zs]ation |
' | while | whilst | whil(?:e|st) |
' | wilfull | wilful | wilfull? |
'

Private Function RegExpReplaceWords(SourceString)
    Dim ResultString
    ResultString = SourceString
    
    Dim PatternAndReplaceStringArray
    PatternAndReplaceStringArray = Array( _
        "sub\W*licen[sc]e", _
        "acknowledge?ment", "analog(?:ue)?", "analy[zs]e", _
        "art[ie]fact", "authori[zs]ation", "authori[zs]ed", _
        "calib(?:er|re)", "cancell?ed", "capitali[zs]ations", _
        "catalog(?:ue)?", "categori[zs]e", "cent(?:er|re)", _
        "copyright\W+(?:hold|own)er", "emphasi[zs]ed", _
        "favou?r", "favou?rite", "fulfill?", _
        "fulfill?ment", "initiali[zs]e", "judge?ment", _
        "labell?ing", "labou?r", "licen[sc]e", _
        "maximi[zs]e", "modell?ed", "modell?ing", _
        "non\W*commercial", "offen[sc]e", "optimi[zs]e", _
        "organi[zs]ation", "organi[zs]e", _
        "per\s*cent", "practi[cs]e", "program(?:me)?", _
        "reali[zs]e", "recogni[zs]e", "signall?ing", _
        "utili[zs]ation", "whil(?:e|st)", "wilfull?")
    
    Dim LB
    Dim UB
    LB = LBound(PatternAndReplaceStringArray)
    UB = UBound(PatternAndReplaceStringArray)
    
    Dim Index
    For Index = LB To UB
        Dim PatternAndReplaceString
        PatternAndReplaceString = CStr(PatternAndReplaceStringArray(Index))
        
        ResultString = _
            RegExpReplace( _
                ResultString, _
                PatternAndReplaceString, _
                PatternAndReplaceString)
    Next
    
    RegExpReplaceWords = ResultString
End Function

Private Function RegExpReplace( _
    SourceString, _
    ReplaceString, _
    Pattern)
    
    On Error Resume Next
    
    With GetRegExp()
        .Pattern = Pattern
        .IgnoreCase = True ' 4. Capitalization
        .Global = True
        .MultiLine = False
        RegExpReplace = .Replace(SourceString, ReplaceString)
    End With
End Function

'Attribute VB_Name = "MRegExp"
'Option Explicit

'
' Copyright (c) 2020 Koki Takeyama
'
' Permission is hereby granted, free of charge, to any person obtaining
' a copy of this software and associated documentation files (the "Software"),
' to deal in the Software without restriction, including without limitation
' the rights to use, copy, modify, merge, publish, distribute, sublicense,
' and/or sell copies of the Software, and to permit persons to whom the
' Software is furnished to do so, subject to the following conditions:
'
' The above copyright notice and this permission notice shall be included
' in all copies or substantial portions of the Software.
'
' THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
' IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
' FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
' AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
' LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
' FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
' IN THE SOFTWARE.
'

'
' Microsoft VBScript Regular Expression 5.5
' - VBScript_RegExp_55.RegExp
'

Private RegExpObject

'
' --- RegExp ---
'

'
' GetRegExp
' - Returns a RegExp object.
'

Public Function GetRegExp()
    'Static RegExpObject
    If IsEmpty(RegExpObject) Then
        Set RegExpObject = New RegExp
    End If
    Set GetRegExp = RegExpObject
End Function

'
' === RegExp ===
'

'
' RegExp_Execute
' - Executes a regular expression search against a specified string.
'
' RegExp_Replace
' - Replaces text found in a regular expression search.
'
' RegExp_Test
' - Executes a regular expression search against a specified string
'   and returns a Boolean value that indicates if a pattern match was found.
'

'
' SourceString:
'   Required. The text string upon which the regular expression is executed.
'
' ReplaceString:
'   Required. The replacement text string.
'
' Pattern:
'   Required. Regular string expression being searched for.
'   https://docs.microsoft.com/en-us/previous-versions/windows/internet-explorer/ie-developer/scripting-articles/f97kw5ka(v=vs.84)
'
' IgnoreCase:
'   Optional. The value is False if the search is case-sensitive,
'   True if it is not. Default is False.
'
' GlobalMatch:
'   Optional. The value is True if the search applies to the entire string,
'   False if it does not. Default is False.
'
' MultiLine:
'   Optional. The value is False if the search is single-line mode,
'   True if it is multi-line mode. Default is False.
'

Public Function RegExp_Execute( _
    SourceString, _
    Pattern, _
    IgnoreCase, _
    GlobalMatch, _
    MultiLine)
    
    On Error Resume Next
    
    With GetRegExp()
        .Pattern = Pattern
        .IgnoreCase = IgnoreCase
        .Global = GlobalMatch
        .MultiLine = MultiLine
        Set RegExp_Execute = .Execute(SourceString)
    End With
End Function

Public Function RegExp_Replace( _
    SourceString, _
    ReplaceString, _
    Pattern, _
    IgnoreCase, _
    GlobalMatch, _
    MultiLine)
    
    On Error Resume Next
    
    With GetRegExp()
        .Pattern = Pattern
        .IgnoreCase = IgnoreCase
        .Global = GlobalMatch
        .MultiLine = MultiLine
        RegExp_Replace = .Replace(SourceString, ReplaceString)
    End With
End Function

Public Function RegExp_Test( _
    SourceString, _
    Pattern, _
    IgnoreCase, _
    MultiLine)
    
    On Error Resume Next
    
    With GetRegExp()
        .Pattern = Pattern
        .IgnoreCase = IgnoreCase
        .MultiLine = MultiLine
        RegExp_Test = .Test(SourceString)
    End With
End Function

'
' --- Test ---
'

Sub btnReplace_OnClick
    Dim SourceString
    SourceString = textareaSourceString.innerText
    If SourceString = "" Then
        MsgBox "SourceString: is blank."
        Exit Sub
    End If
    
    Dim Result
    Result = GetMatchingText(SourceString)
    
    textareaResult.innerText = Result
End Sub
-->
</script>

</head>

<body style="margin:0">

<table width="100%">

<colgroup>
<col width="100pt"></col>
<col></col>
</colgroup>

<tr>
<td>SourceString:</td>
<td><textarea id="textareaSourceString" rows="10" cols="50" style="width:100%"></textarea></td>
</tr>

<tr>
<td></td>
<td>
<button id="btnReplace">Replace</button>
</td>
</tr>

<tr>
<td>Result:</td>
<td><textarea id="textareaResult" rows="10" cols="50" style="width:100%"></textarea></td>
</tr>

</table>

</body>

</html>
